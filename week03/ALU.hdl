// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    // Put you code here:

    //zx & nx (sel nxzx: 00:x=x, 01:x=false, 10:x=notx, 11:x=true)
    Not16(in=x, out=notx);
    Mux4Way16(a=x, b=false, c=notx, d=true, sel[0]=zx, sel[1]=nx, out=x0); 
    //zy & ny (sel nyzy: 00:y=y, 01:y=false, 10:y=noty, 11:y=true)
    Not16(in=y, out=noty);
    Mux4Way16(a=y, b=false, c=noty, d=true, sel[0]=zy, sel[1]=ny, out=y0); 
    
    //f0 (&)
    And16(a=x0, b=y0, out=f00);
    //f1 (x+y)
    Add16(a=x0, b=y0, out=f10);
    Mux16(a=f00, b=f10, sel=f, out=f0);

    //no (!out)
    Not16(in=f0, out=nf0);
    //out[0..7] and out[8..15] used to check for zr
    //out[15] used for ng to check for negative (Most significant digit) for signed values
    Mux16(a=f0, b=nf0, sel=no, out=out, out[0..7]=zr00, out[8..15]=zr01, out[15]=ng0); 
    

    //flags
    //zr (check for 0)
    Or8Way(in=zr00, out=zr0);
    Or8Way(in=zr01, out=zr1);
    Or(a=zr0, b=zr1, out=nzr);
    Not(in=nzr, out=zr);
    //ng (check most significant bit for sign)
    And(a=ng0, b=true, out=ng); 

}


















